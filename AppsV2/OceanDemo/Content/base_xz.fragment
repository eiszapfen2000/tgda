#version 150

in vec3 out_ws_pos;
in vec3 out_normal;

#define MATH_PI 3.14159

void main()
{
    vec3 lightPos = vec3(50.0, 20.0, 50.0);
    vec3 lightDir = lightPos - out_ws_pos;
    vec3 viewDir  = cameraPosition - out_ws_pos;
    vec3 l        = normalize(lightDir);
    vec3 v        = normalize(viewDir);
    vec3 n        = normalize(out_normal);
    vec3 h        = normalize(v + l);
    vec3 r        = 2.0 * dot(l, n) * n - l;

    vec3 c_light = vec3(1.0, 1.0, 1.0);
    vec3 c_diff  = vec3(0.1, 0.5, 0.9);
    vec3 c_spec  = vec3(0.02);
    float alpha_p = 1.0;
    float specular_normalization = (alpha_p + 2.0) / (2.0 * MATH_PI);
    float brdf_factor = MATH_PI / 4.0;

    float r_dot_l = clamp(dot(r, l), 0.0, 1.0);
    float n_dot_l = clamp(dot(n, l), 0.0, 1.0);
    float n_dot_h = clamp(dot(n, h), 0.0, 1.0);
    float l_dot_h = clamp(dot(l, h), 0.0, 1.0);

    vec3 f_schlick = c_spec + ((vec3(1.0) - c_spec) * pow(1.0 - l_dot_h, 5.0));

    vec3 diffuse_term = c_diff;
    //diffuse_term = vec3(0.0);
    vec3 specular_term = f_schlick * pow(n_dot_h, alpha_p) * specular_normalization * brdf_factor;
    vec3 L_zero = (diffuse_term + specular_term) * (c_light * n_dot_l);

    // from linear to gamma corrected
    vec3 diffuse = pow(L_zero, vec3(1.0 / 2.2));
    target = vec4(diffuse, 1.0);
}

