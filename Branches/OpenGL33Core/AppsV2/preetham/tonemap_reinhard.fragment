#version 150

in vec2 out_texcoords;

vec3 srgbEncode(vec3 color){
   float r = color.r < 0.0031308 ? 12.92 * color.r : 1.055 * pow(color.r, 1.0/2.4) - 0.055;
   float g = color.g < 0.0031308 ? 12.92 * color.g : 1.055 * pow(color.g, 1.0/2.4) - 0.055;
   float b = color.b < 0.0031308 ? 12.92 * color.b : 1.055 * pow(color.b, 1.0/2.4) - 0.055;
   return vec3(r, g, b);
}

void main()
{
    float averageLogLuminance
         = textureLod(logLuminance, vec2(0.5, 0.5), averageLuminanceLevel).r;

    float averageLuminance = exp(averageLogLuminance);

    vec3 XYZ = texture(XYZColor, out_texcoords).xyz;

    vec3 xyY;
    xyY.z = XYZ.y;                           // copy luminance Y
    xyY.x = XYZ.x / (XYZ.x + XYZ.y + XYZ.z); // x = X / (X + Y + Z)
    xyY.y = XYZ.y / (XYZ.x + XYZ.y + XYZ.z); // y = Y / (X + Y + Z)

    float invWhiteLuminance = 1.0 / (whiteLuminance * whiteLuminance);
    float L = (key / averageLuminance) * xyY.z;
    float L_d = (L * ((L * invWhiteLuminance) + 1.0)) / (L + 1.0);

    xyY.z = L_d;

    // xyY -> XYZ conversion
    XYZ.r = xyY.z * xyY.x / xyY.y;                  // X = Y * x / y
    XYZ.g = xyY.z;                                  // copy luminance Y
    XYZ.b = xyY.z * (1.0f - xyY.x - xyY.y) / xyY.y; // Z = Y * (1-x-y) / y

    vec3 lsRGB = XYZToLinearsRGB * XYZ;

	/*	
	lsRGB = max(lsRGB, 0.0);

	float maxValue = max(lsRGB.r, max(lsRGB.g, lsRGB.b));
	if (maxValue > 1.0)
	{
		lsRGB = min(lsRGB / maxValue, vec3(1.0));
	}
	*/

	//target = vec4(pow(lsRGB, vec3(1/2.2)), 1.0);
    target = vec4(srgbEncode(lsRGB), 1.0);
}
